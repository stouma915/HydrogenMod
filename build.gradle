buildscript {
    repositories {
        maven {
            url = "https://maven.minecraftforge.net"
        }

        mavenCentral()
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:5.1.+"
    }
}

apply plugin: "scala"
apply plugin: "net.minecraftforge.gradle"

version = "2.0.0"
group = "net.stouma915.hydrogenmod"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: "official", version: "1.16.5"

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                hydrogenmod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                hydrogenmod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'hydrogenmod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                hydrogenmod {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main {
        scala {
            srcDirs = ["src/main/scala"]
        }

        java {
            srcDirs = []
        }

        resources {
            srcDir "src/generated/resources"
        }
    }
}

configurations {
    includeInBin

    implementation.extendsFrom(includeInBin)
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:1.16.5-36.2.34"
    includeInBin "org.scala-lang:scala3-library_3:3.1.1"
}

processResources {
    configurations.includeInBin.canBeResolved(true)

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from configurations.includeInBin.collect {
        it.isDirectory() ? it : zipTree(it)
    }
}

jar {
    archiveName("hydrogenmod-${project.version}.jar")
}
